apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion Integer.parseInt(rootProject.ANDROID_SDK_VERSION)
    buildToolsVersion rootProject.ANDROID_BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId rootProject.GITPO_APPLICATION_ID
        testApplicationId rootProject.GITPO_APPLICATION_ID + ".androidtest"
        minSdkVersion Integer.parseInt(rootProject.MIN_ANDROID_SDK_VERSION)
        targetSdkVersion Integer.parseInt(rootProject.ANDROID_SDK_VERSION)
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "TYPE", "ANDROID_VERSION"

    variantFilter { variant ->
        //Skip build combinations:
        // - L21Release because l21 flavor using only for debug builds
        // - StageRelease, we never release stage packages
        if (variant.buildType.name == "release") {
            if (variant.flavors[1].name == "l21" || variant.flavors[0].name == "stage") {
                variant.ignore = true
            }
        }
    }

    productFlavors {

        stage {
            dimension "TYPE"

            applicationId rootProject.GITPO_APPLICATION_ID + ".test"
            testApplicationId rootProject.GITPO_APPLICATION_ID + ".test.androidtest"
        }

        prod {
            dimension "TYPE"
        }

        l16 {
            dimension "ANDROID_VERSION"
        }

        l21 {
            dimension "ANDROID_VERSION"
            //API level 21 - first API level with support of ART-supported multidex output
            minSdkVersion 21
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    provided 'com.squareup.dagger:dagger-compiler:1.2.5'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:$rootProject.ANDROID_SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$rootProject.ANDROID_SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$rootProject.ANDROID_SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$rootProject.ANDROID_SUPPORT_LIBRARY_VERSION"
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.squareup:otto:1.3.8'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.jakewharton:butterknife:8.5.1'
    implementation 'com.squareup.dagger:dagger:1.2.5'
    annotationProcessor  'com.squareup.dagger:dagger-compiler:1.2.5'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    testCompile 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}
repositories {
    mavenCentral()
    google()
}
